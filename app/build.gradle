apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 33
    //buildToolsVersion "23.0.0 rc2"


    defaultConfig {
        applicationId "ir.blog.mrcoder.darsadyar"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 9
        versionName "1.3"
    }
    signingConfigs {
        release {
            file("../signing.properties").with { propFile ->
                if (propFile.canRead()) {
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))

                    storeFile file(properties['keystore.path'])
                    storePassword properties['keystore.password']
                    keyAlias properties['keystore.key.alias']
                    keyPassword properties['keystore.key.password']
                } else {
                    println 'Unable to read signing.properties'
                }
            }
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors{
        original{
            buildConfigField('String', 'ADAD_BASE64_KEY', "\"" + getAdadBase64Key() + "\"")
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    lint {
        checkReleaseBuilds false
    }
    namespace 'ir.blog.mrcoder.darsadyar'
}

def getAdadBase64Key() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("adad.key");
}

dependencies {
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/adad-client-3.1.jar')
    implementation 'com.android.support:design:26.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
